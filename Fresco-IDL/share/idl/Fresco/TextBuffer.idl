/*$Id$
 *
 * This source file is a part of the Berlin Project.
 * Copyright (C) 1999 Stefan Seefeld <stefan@berlin-consortium.org> ,
 *               2001 Tobias Hunger <tobias@berlin-consortium.org>
 * http://www.berlin-consortium.org
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Library General Public
 * License as published by the Free Software Foundation; either
 * version 2 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Library General Public License for more details.
 *
 * You should have received a copy of the GNU Library General Public
 * License along with this library; if not, write to the
 * Free Software Foundation, Inc., 675 Mass Ave, Cambridge,
 * MA 02139, USA.
 */
#ifndef _Warsaw_TextBuffer_idl
#define _Warsaw_TextBuffer_idl

#include <Warsaw/Subject.idl>
#include <Warsaw/Observer.idl>
#include <Warsaw/Types.idl>

module Warsaw
{
  //. The TextBuffer is used to store Unicode encoded strings of text.
  //. It can be observed by other Objects which gets notified whenever
  //. the string is changed.
  interface TextBuffer : Subject
    {
      
      //. These are the changes that can happen to a TextBuffer:
      enum ChangeType {insert, remove, cursor};

      //. The possible orders in which the String in the TextBuffer can be.
      enum StringOrder {memory_order, visual_order};
      
      //. This structure is passed to observers whenever the
      //. TextBuffer changed.
      struct Change
      {
	  ChangeType type;    //.< What happened?
	  unsigned long pos;  //.< Where did it happen?
	  long len;           //.< How long was the change? This might be a negative number.
	  boolean visual;     //.< Did this change happen to the string in memory or visual order?
      };
      
      //. The length of the TextBuffer.
      readonly attribute long size;
      //. The contents of the TextBuffer.
      readonly attribute Unistring value;
      //. Return or set the position of the cursor.
      attribute long position;
      //. Get a substring of the TextBuffer starting at pos
      //. that is len characters long.
      Unistring get_chars(in unsigned long pos, in unsigned long len);
      //. Move the cursor forward for one character.
      void forward();
      //. Move the cursor backwards for one character.
      void backward();
      
      //. FIXME: What does this do?! -- tobias
      void shift(in long d);
      //. Insert a character at the current position.
      //. Allways use insert_string since this
      //. might lead to unexspected results when adding characters with
      //. codevalues higher then 0xFFFF!
      void insert_char(in Unichar u);
      //. Insert a string into the Buffer. It gets inserted at the
      //. current cursor position.
      void insert_string(in Unistring u);
      //. Remove d characters backward.
      //. This is relative to the current cursor position.
      void remove_backward(in long d);
      //. Remove d characters forward.
      //. This is relative to the current cursor position.
      void remove_forward(in long d);
      //. Clear the buffer. It will be empty afterwards.
      void clear();
    };
};

#endif
