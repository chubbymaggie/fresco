/*$Id$
 *
 * This source file is a part of the Berlin Project.
 * Copyright (C) 1998 Graydon Hoare <graydon@pobox.com> 
 * Copyright (C) 1999, 2000 Stefan Seefeld <stefan@berlin-consortium.org> 
 * http://www.berlin-consortium.org
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Library General Public
 * License as published by the Free Software Foundation; either
 * version 2 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Library General Public License for more details.
 *
 * You should have received a copy of the GNU Library General Public
 * License along with this library; if not, write to the
 * Free Software Foundation, Inc., 675 Mass Ave, Cambridge,
 * MA 02139, USA.
 */
#ifndef _Warsaw_CommandKit_idl
#define _Warsaw_CommandKit_idl

#include <Warsaw/Types.idl>
#include <Warsaw/Kit.idl>
#include <Warsaw/Command.idl>
#include <Warsaw/Telltale.idl>
#include <Warsaw/Selection.idl>

module Warsaw
{
  interface BoundedValue;
  interface BoundedRange;
  interface TextBuffer;
  interface StreamBuffer;
  interface Command;
  
  //. The CommandKit is a collection of strategy objects. They form the
  //. basis of any control protocol between widgets and subjects. The
  //. Kit also encapsulates the constructors for command dispatchers
  //. (a.k.a. reactors), which you can think of as threading strategies
  //. if that suits you.
  interface CommandKit : Kit
    {
      //. This Command dumps the given string to stdout before calling
      //. into the given Command. The given text will be dumped again
      //. after the given Command returns. 
      Command debugger(in Command c, in string s);
      //. This Command just dumps the given text to stdout whenever this Command
      //. gets executed. 
      Command log(in string t);
      //. Create a MacroCommand.
      MacroCommand composite();

      //. This constraint will make sure that a maximum of one of the
      //. Telltales will be match the given Mask.
      TelltaleConstraint exclusive(in Telltale::Mask m);
      //. This constraint will make sure that at least one of the
      //. Telltales will be set.
      TelltaleConstraint selection_required();
      // FIXME: Why is there no exclusive_required()? -- tobias

      //. Get a unconstrained Telltale.
      Telltale     normal_telltale();
      //. Get a Telltale with the given constraint set.
      Telltale     constrained_telltale(in TelltaleConstraint c);
      //. Get a Selection that enforces the given Policy.
      Selection    group(in Selection::Policy p);
      //. Get a BoundedValue with a lower bound of l, a upper boundary of u,
      //. a value of v, a step size of s and a pagesize of p.
      BoundedValue bvalue(in Coord l, in Coord u, in Coord v, in Coord s, in Coord p);
      //. Get a BoundedRange with a lower bound of l, a upper boundary of u,
      //. a inner range with the bounds of lv and uv, a step size of s and
      //. a pagesize of p.
      BoundedRange brange(in Coord l, in Coord u, in Coord lv, in Coord uv, in Coord s, in Coord p);
      //. Get a TextBuffer.
      TextBuffer   text();
      //. Get a StreamBuffer.
      StreamBuffer stream(in long buffer);
    };
};

#endif
