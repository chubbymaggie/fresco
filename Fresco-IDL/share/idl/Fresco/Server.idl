/*$Id$
 *
 * This source file is a part of the Berlin Project.
 * Copyright (C) 1998 Graydon Hoare <graydon@pobox.com> 
 * Copyright (C) 2000 Stefan Seefeld <stefan@berlin-consortium.org> 
 * http://www.berlin-consortium.org
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Library General Public
 * License as published by the Free Software Foundation; either
 * version 2 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Library General Public License for more details.
 *
 * You should have received a copy of the GNU Library General Public
 * License along with this library; if not, write to the
 * Free Software Foundation, Inc., 675 Mass Ave, Cambridge,
 * MA 02139, USA.
 */
#ifndef _Warsaw_Server_idl
#define _Warsaw_Server_idl

#include <Warsaw/Types.idl>
#include <Warsaw/Kit.idl>

module Warsaw
{
  interface ClientContext;

  //. The ServerContext is an object you pick up from the berlin
  //. server's root ContextManager, and which you use to create all
  //. generic interfaces (kits, plugins, etc).
  interface ServerContext
    {
      //. The ClientContext that is associated with this
      readonly attribute ClientContext client;
      //. This is the entry point to generic object creation on a server.
      Kit resolve(in string type, in Kit::PropertySeq attr)
	raises (SecurityException, CreationFailureException);
      
      //. Add the given Object to the serverwide map of available singletons.
      //. It can get accessed later by the given name.
      //. SecurityException is raised if the client is not permitted to
      //. install Singletons.
      //. SingletonFailureException is raised whenever you try to add a
      //. singleton with a name that allready exists.
      void set_singleton(in string name, in Object singleton) 
	raises (SecurityException, SingletonFailureException);
      //. Remove the singleton with the given name from the serverwide map of
      //. available singletons.
      //. SecurityException is raised if the client is not permitted to
      //. remove the given singleton.
      //. SingletonFailureException is raised whenever you try to remove a
      //. a name that does not exist in the map.
      void remove_singleton(in string name)
	raises (SecurityException, SingletonFailureException);
      //. Get the Singleton-Object that is associated to the given name.
      //. SecurityException is raised if the client is not permitted to
      //. access the Singleton.
      //. SingletonFailureException is raised whenever you try to request a
      //. singleton with a name that does not exist in the map.
      Object get_singleton(in string name) 
	raises (SecurityException, SingletonFailureException);
    };

  //. This is the interface of the only Object that can be looked up in
  //. the CORBA nameservice: This is what a client sees.
  interface Server
    {
      //. create_server_context() is the method used by a Berlin client to
      //. connect to the Berlin server.
      //. The server examines the given ClientContext and if it has
      //. sufficient permissions the server will return a ServerContext which
      //. can then be used to access the server's resources.
      ServerContext create_server_context(in ClientContext C) raises (SecurityException);
    };
};

#endif
